/* TODO:
 * - dynamic tags
 */


@context tracker from "./tokens.js"

@external propSource inkHighlight from "./highlight.js"

@top Story {
    statement*
}

@skip {
    space | LineComment | BlockComment
}


@precedence {
    choiceCondition,
    inlineLogic,
    times @left,
    plus @left,
    list @left,
    rel @left,
    and @left,
    or @left

}

// If we were building a tree, we'd try to keep what's between knot & stitch headers in the correct block but we're only doing syntax highlighting so we take a simpler approach.

ownLineStatement {
    // All these take one line
    (multiDivert eol)
    | AuthorWarning
    | ListDeclaration
    | VariableDeclaration
    | ConstDeclaration
    | ExternalDeclaration
    | IncludeStatement
    | LogicLine
    | Choice
    | (MixedTextAndLogic eol)
    // We also consume empty lines
    | "\n" // eol does loop infinitely (why?)
}

statement {
    ownLineStatement
    | KnotDeclaration
    | StitchDeclaration
    | Gather
}

innerStatement {
    ownLineStatement
}

KnotDeclaration {
    KnotTitleEquals
    ( kw<"function"> Identifier | Identifier )
    BracketedKnotDeclArguments?
    KnotTitleEquals?
    eol
}

StitchDeclaration {
    StitchTitleEquals
    ( kw<"function"> Identifier | Identifier )
    BracketedKnotDeclArguments?
    StitchTitleEquals?
    eol
}


// innerBlockLevelStatements {
//     (innerBlockLevelStatement !innerBlockBreak)*
// }

// innerBlockBreak {
//     ParseDashNotArrow | "}" | StitchDeclaration | KnotDeclaration
// }

// innerBlockLevelStatement {
//       (MultiDivert eol)
//     | Choice
//     | AuthorWarning
//     | ListDeclaration
//     | VariableDeclaration
//     | ConstDeclaration
//     | ExternalDeclaration
//     | IncludeStatement
//     | LogicLine
//     | (MixedTextAndLogic eol)
// }


multiDivert {
    startThread
    | divertArrowOrTunnelOnwards (divertIdentifierWithArguments divertArrowOrTunnelOnwards)* divertIdentifierWithArguments?
}

singleDivert {
    // "- ->" divert with no name for fallback choice
    divertOp<"->">
    // "-> knit.stitch(foo)" simple divert
   | divertOp<"->"> divertIdentifierWithArguments
}

startThread {
    divertOp<"<-"> divertIdentifierWithArguments
}

divertArrowOrTunnelOnwards {
    divertOp<"->"> | divertOp<"->->">
}

divertIdentifierWithArguments {
    DotSeparatedDivertPathComponents ExpressionFunctionCallArguments?
}

DotSeparatedDivertPathComponents {
    Identifier ("." Identifier)*
}



Choice {
    Bullets
    ChoiceName?
    ChoiceSingleCondition* // FIXME: parsed as inline logic // FIXME: handle eol between conditions
    MixedTextAndLogic?
    (ChoiceBrackets MixedTextAndLogic?)?
    // multiDivert? // FIXME
    eol
}

ChoiceBrackets {
    // The closing ] is used as a choice terminator so it's captured there
    "[" MixedTextAndLogicInChoiceBrackets? choiceClosingBracketWithOptionalTag
}

ChoiceSingleCondition {
    "{" expression !choiceCondition "}"
}

_MixedTextAndLogic<content, logic> {
    (content | logic)+ // multiDivert? // FIXME
}

MixedTextAndLogic {
    _MixedTextAndLogic<ContentText, inlineLogic<expression> | Glue | Tag>
}

MixedTextAndLogicInChoiceBrackets {
    _MixedTextAndLogic<ContentText, inlineLogic<expression>>
}

MixedTextAndLogicInSequence {
    _MixedTextAndLogic<ContentText, inlineLogic<expressionNoVar> | Glue | Tag>
}

inlineLogic<expr> {
     "{" innerLogic<expr> !inlineLogic "}"
}

innerLogic<expr> {
    Sequence
    | ConditionExpression InnerConditionalContent_InlineAllowed
    | InnerConditionalContent_InlineDisallowed
    // | InnerSequence // FIXME: why is this in ink parser?
    | expr
}


Sequence {
     SequenceTypeWordAnnotation+ ":" innerSequenceObjects
    | SequenceOp innerInlineSequenceObjects
    // When there's no operator, there must be at least one pipe
    | innerInlineSequenceObjects_MandatoryPipe
}

SequenceOp {
    // We use an external tokenizer only to have high precedence than contentText :-(
    sequenceSymbol
}

SequenceTypeWordAnnotation {
    kw<"once"> | kw<"cycle"> | kw<"shuffle"> | kw<"stopping">
}

innerSequenceObjects {
    "\n" innerMultilineSequenceObjects
    | innerInlineSequenceObjects
}

innerInlineSequenceObjects {
    // M or M|M|M or M||||M|M or ||…
    (MixedTextAndLogicInSequence? "|")* MixedTextAndLogicInSequence?
}

innerInlineSequenceObjects_MandatoryPipe {
    // M|M|M or M||||M|M or ||…
    (MixedTextAndLogicInSequence? "|")+ MixedTextAndLogicInSequence?
}

innerMultilineSequenceObjects {
    SingleMultilineSequenceElement+
}

SingleMultilineSequenceElement {
    // !"->"
    "-"
    innerStatement+
}

InnerConditionalContent_InlineAllowed {
    "\n" (MultilineConditionalBranches | BlockAndElse)
    | InlineConditionalBranches
}

InnerConditionalContent_InlineDisallowed {
    "\n" (MultilineConditionalBranches | BlockAndElse)
}

MultilineConditionalBranches {
    // "\n"*
    SingleMultilineCondition+
}

SingleMultilineCondition {
    "-"
    (ElseExpression | ConditionExpression)
    innerStatement+
}

ElseExpression {
    kw<"else"> ":"
}

ConditionExpression {
    expression ":"
}

InlineConditionalBranches {
    MixedTextAndLogic ("|" MixedTextAndLogic)*
}

BlockAndElse {
    innerStatement+ SingleMultilineCondition // FIXME: check -else
}

InnerSequence {
    SequenceTypeAnnotation? innerSequenceObjects
}

ExpressionFunctionCallArguments {
    "(" expression ("," expression)* ")"
}

LogicLine {
    "~" (expression | ReturnStatement | TempDeclarationOrAssignment)? eol
}

ReturnStatement {
    kw<"return"> expression?
}

TempDeclarationOrAssignment {
    kw<"temp">? Identifier ("=" | "-=" | "+=") expression
}

VariableDeclaration {
    kw<"VAR"> Identifier DeclarationEqual expression eol
}

ConstDeclaration {
    kw<"CONST"> Identifier DeclarationEqual expression eol
}

ExternalDeclaration {
    kw<"EXTERNAL"> FunctionIdentifier BracketedKnotDeclArguments eol
}

IncludeStatement {
    kw<"INCLUDE"> Included eol
}

ChoiceName {
    BracketedName
}

BracketedName {
    "(" Identifier ")"
}

ListDeclaration {
    kw<"LIST"> Identifier DeclarationEqual ListDefinition eol
}

ListDefinition {
    ListElementDefinition ( "," ListElementDefinition)*
}

ListElementDefinition {
    Identifier
    | Identifier DeclarationEqual Integer
    | "(" Identifier ")"
    | "(" Identifier DeclarationEqual Integer ")"
    | "(" Identifier ")" DeclarationEqual Integer
}
BracketedKnotDeclArguments {
    "(" flowDeclArgument ("," flowDeclArgument)* ")"
}

flowDeclArgument {
    // Possible forms:
    //  name
    //  -> name      (variable divert target argument)
    //  ref name
    //  ref -> name  (variable divert target by reference)
    kw<"ref"> "->" Identifier
    | kw<"ref">  Identifier
    | "->" Identifier
    | Identifier
}


// For testing purposes
@top Expressions {
    (expression eol)+
}

expression {
    BinaryExpression | unaryExpression
}

expressionNoVar {
    BinaryExpression | unaryExpressionNoVar
}

// Expose Binary in the tree so we can test precedence
BinaryExpression {
    expression !times (ArithOp<"/" | "%" | "*" | kw<"mod">>) expression
    | expression !plus ArithOp<"+" | "-"> expression
    | expression !list ListOp<"?" | "!?" | "^" | kw<"has"> | kw<"hasnt">> expression
    | expression !rel CompareOp<"<=" |"<"| ">=" |">" | "==" | "!=" > expression
    | expression !and (LogicOp<"&&" | kw<"and">>) expression
    | expression !or (LogicOp<"||" | kw<"or">>) expression
}

unaryExpression {
    unaryExpressionNoVar
    | (ExpressionVariableName ("++" | "--")?)
}

unaryExpressionNoVar {
    expressionLiteral
    | ExpressionDivertTarget
    | ExpressionList
    | ExpressionFunctionCall
    | (minus | "!" | kw<"not">) unaryExpression
    // FIXME:
    // | ExpressionParen
}

ExpressionDivertTarget {
    singleDivert
}

expressionLiteral {
    Float
    | Integer
    | kw<"true"> | kw<"false">
    | String
}

ExpressionFunctionCall {
    Identifier ExpressionFunctionCallArguments
}

ExpressionVariableName {
    Identifier ("." Identifier)?
}

ExpressionParen {

    "(" expression ")"
}

ExpressionList {

    "(" ListMember ("," ListMember)* ")"
}

ListMember {
    Identifier ("." Identifier)?
}


Gather {
    GatherDashes BracketedName?
}

@local tokens {
    todoEnd { eol }
    @else todoContent
}

@skip {} {
    AuthorWarning {
        kw<"TODO"> todoContent* todoEnd
    }
}


// Specific Identifier alias for highlighting
FunctionIdentifier {
    Identifier
}



ArithOp<expr> { expr }
LogicOp<expr> { expr }
ListOp<expr> { expr }
CompareOp<expr> { expr }



DeclarationEqual { "=" }



/********************************/
/* Interpolated strings in code */
/********************************/

@local tokens {
    interpolationStart[@name="{"] { "{" }
    stringEnd { '"' }
    StringEscape { "\\" _ }
    @else stringContent
}

@skip {} {
    String {
        '"' (StringEscape | stringContent | Interpolation)* stringEnd
    }
}

Interpolation { interpolationStart expression "}" }

/************/
/* Comments */
/************/

@local tokens {
    blockCommentEnd { "*/" }
    blockCommentNewline { "\n" }
    @else blockCommentContent
}

@skip {} {
    BlockComment {
        "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd
    }
}


@external tokens comments from "./tokens.js" { lineComment }

LineComment {
    lineComment
}


/********/
/* Tag */
/********/

@external tokens tags from "./tokens.js" { contentTag }

Tag {
    contentTag
}

choiceClosingBracketWithOptionalTag {
    "]" | choiceTagBracket
}

@local tokens {
    choiceTagEnd[@name="]"] { "]" }
    @else choiceTagContent
}

@skip {} {
    choiceTagBracket {
        ChoiceTag choiceTagEnd
    }
    ChoiceTag {
        "#" choiceTagContent+
    }
}


@external tokens sequenceSymbols from "./tokens.js" { sequenceSymbol }

/****************/
/* Content text *
/****************/


@tokens {
    space { $[ \t]+ }
    eol { "\n" | @eof }

    Glue { "<>" }
    // Number { @digit+ }
    // Letter { @asciiLetter+ }
    Identifier {
        // FIXME: ExtendIdentifierCharacterRanges
        $[A-Za-z] $[A-Za-z0-9_]*
    }
    DivertOperator {
        "->" | "<-" | "->->"
    }

    @precedence { DivertOperator, minus }

    Included {
        // FIXME: check real restrictions
        $[A-Za-z0-9_]+ ".ink"
    }
    Bullets {
        "*"+ | "+"+
    }

    GatherDashes {
        ("-" space?)+
    }

    KnotTitleEquals {
        "=" "="+
    }
    StitchTitleEquals {
        "="
    }

    @precedence { KnotTitleEquals, StitchTitleEquals }

    @precedence { DivertOperator, GatherDashes, "-" }

    Integer {
        "-"? $[0-9]+
    }
    Float {
        "-"? $[0-9]+ "." $[0-9]+
    }

    @precedence { Float, Integer, minus }

    minus[@name="-"] { "-" }

    // We want those in the tree
    "(" ")" "{" "}" "[" "]"
    "=" "+=" "-="
    "~" "!" "," "."
    "++" "--"
    "*" "/" "+" "-" "%"
    "?" "!?" "^"
    "<=" "<" ">=" ">"  "==" "!="
    "&&" "||"
    "!" "&" "~" "$" "|"
    ":"
 }

kw<term> { @specialize[@name={term}]<Identifier, term> }
divertOp<term> { @specialize[@name={term}]<DivertOperator, term> }

@external tokens contentText from "./tokens.js" { content }

ContentText {
    content
}
